; mode: 1-> CGA 0->EGA
; bfix: 1-> brown 0-> no brown (dark yellow)
chip CONVERTER GAL16V8

green_I=1 red_I=2 blue_I=3 red=4 hsync=5 green=6 vsync=7 blue=8 mode=9 GND=10 bfix=11 ncsync=12
csync=13 Ored_I=14 Ored=15 Ogreen_I=16 Ogreen=17 Oblue_I=18 Oblue=19 VCC=20

equations

Oblue = blue
Oblue.oe = VCC

; Intensity for blue depends if we are
; in CGA or EGA mode (CGA mode uses the green intensity for all colours)
Oblue_I = /mode * blue_I + 
	      mode * green_I
Oblue_I.oe = VCC

; The brown fix is applied in CGA mode, when R and G are high, B is low and the INTENSITY is low.
; The Green component gets lowered artificially (effectively swapped with the intensity).
Ogreen = green * /bfix +
         green * blue +
         green * /red +
         green * green_I +
         green * /mode
Ogreen.oe = VCC

Ogreen_I = mode * /blue * green * red * bfix + 
	       green_I
Ogreen_I.oe = VCC

Ored = red
Ored.oe = VCC

Ored_I = /mode * red_I + 
         mode * green_I
Ored_I.oe = VCC

csync = vsync * /hsync + 
	    /vsync * hsync
csync.OE = VCC

ncsync = /csync
ncsync.OE = VCC

